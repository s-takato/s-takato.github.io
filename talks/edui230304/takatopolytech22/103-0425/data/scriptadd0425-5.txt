if(scriptaddstart==1,

////%Dotprod start////
Dotprod(vec1,vec2):=(
//help:Dotprod(vec1,vec2);
  regional(v1,v2,tmp);
  if(ispoint(vec1),v1=vec1.xy,v1=vec1);
  if(ispoint(vec2),v2=vec2.xy,v2=vec2);
  v1*v2;
);
///%Anglemark start////
Anglemark(plist):=Anglemark(plist,[]);
Anglemark(Arg1,Arg2):=( // 2015.04.28 from
  regional(nm,plist,options,tmp);
  if(isstring(Arg1),
    nm=Arg1;
    plist=Arg2;
    Anglemark(nm,plist,[]);
  ,
    plist=Arg1;
    options=Arg2;
    tmp=Textformat(plist,10);
    tmp=replace(tmp,",","");
    nm=substring(tmp,1,length(tmp)-1);
    Anglemark(nm,plist,options);
  );
);// to
Anglemark(nm,plist,optionsorg):=(
//help([A,B,C],["E=\theta",2]);
//help:Anglemark("1",[A,B,C],["E=1.2,\theta",2]);
// help:Anglemark("1",[A,B,2*pi]);
//help:Anglemark(options=[size,"E/L=(sep,)letter"]);
  regional(name,options,Out,pB,pA,pC,Ctr,ra,sab,sac,ratio,opstr,Bname,Bpos,Bstr,color,color4,
       Brat,tmp,tmp1,tmp2,Num,opcindy,Ltype,eqL,realL,Rg,Th,Noflg,Msg,scaley);
  name="ag"+nm;
  options=optionsorg; //200619
  ra=0.5;
  tmp=Divoptions(options);
  Ltype=tmp_1;
  Noflg=tmp_2;
  color=tmp_(length(tmp)-2);color4=Colorrgb2cmyk(color); //200629
  opcindy=tmp_(length(tmp));
  eqL=tmp_5;
  realL=tmp_6;
  Bname="";
  Brat=1.2; //180530
  Num=20;
  Msg="Y";
  // Msg="N"; //only ketjs
  opstr="";
  if(length(realL)>0,
    ra=realL_1*ra;
    opstr=","+text(realL_1);//180530
  );
  forall(eqL,
    tmp=indexof(#,"="); //200617[2lines]
    tmp=[substring(#,0,tmp-1),substring(#,tmp,length(#))];
    tmp1=Toupper(substring(tmp_1,0,1));
    if((tmp1=="L")%(tmp1=="E"),
      if(tmp1=="L",Bname="L");
      if(tmp1=="E",Bname="E");
      tmp2=tmp_2;
      tmp=indexof(tmp2,",");
      if((tmp==0)%(substring(tmp2,tmp-2,tmp-1)=="\"), //200710
        Bstr=tmp2;
      ,
        Bstr=substring(tmp2,tmp,length(tmp2)); //200201
        tmp1=parse(substring(tmp2,0,tmp-1));
        Brat=tmp1*Brat;
      );
      options=remove(options,[#]); //200619
    );
    if(tmp1=="M", //190206from
      Msg=Toupper(substring(tmp_2,0,1));
    ); //190206to
  );
  pB=Pcrd(plist_1); pA=Pcrd(plist_2); pC=Pcrd(plist_3);
  scaley=SCALEY; //191231[2lines]
  SCALEY=1;
  sab=pB-pA;
  Ctr=pA;
  if((length(plist_3)>1)%(ispoint(plist_3)), //180506from
    sac=pC-pA;
    Rg=[arctan2(sab)+0,arctan2(sac)+0];
  ,
    sac=pB-pA;
    Rg=[arctan2(sab)+0,arctan2(sab)+plist_3];   
  ); //180506to
  if(Rg_2<Rg_1,Rg_2=Rg_2+2*pi);
  Out=[];
  if(ra>min(|sab|,|sac|),  // 16.12.29
    println("  segments too short");
  ,
    forall(0..Num,
      Th=Rg_1+#*(Rg_2-Rg_1)/Num;
      Out=append(Out,Ctr+ra*[cos(Th),sin(Th)]);
    );
    Th=(Rg_1+Rg_2)/2; //16.10.31from[moved]
    if(Noflg<3,
      if(Msg=="Y", //190206
        println("generate anglemark "+name+" and m"+name); //200610
      );
      tmp1=apply(Out,Pcrd(#));
      tmp=name+"="+Textformat(tmp1,10)+";";
      parse(tmp);
      tmp=Textformat(plist,10); //no ketjs on
      tmp1=substring(tmp,1,length(tmp)-1);
      tmp=name+"=Anglemark("+tmp1+opstr+")";
      GLIST=append(GLIST,tmp); //no ketjs off
    );
    if(Noflg<3, //190818
      if(isstring(Ltype),
        if((Noflg==0)&(color4!=KCOLOR), //180904 //no ketjs on
          Texcom("{");Com2nd("Setcolor("+color4+")");//180722
        ); //no ketjs off
        Ltype=Getlinestyle(text(Noflg)+Ltype,name);
        if((Noflg==0)&(color4!=KCOLOR), //180904 //no ketjs on
          Texcom("}");//180722
        ); //no ketjs off
      ,
        if(Noflg==1,Ltype=0);
      );
      GCLIST=append(GCLIST,[name,Ltype,opcindy]);
    );
  );
  SCALEY=scaley;  //191231
  tmp1=Ctr+Brat*ra*[cos(Th),sin(Th)];
  Bpos=LLcrd(tmp1);
  options=remove(options,realL); //200619[3lines]
  if(Bname=="L",Letter(Bpos,"c",Bstr,options)); 
  if(Bname=="E",Expr(Bpos,"c",Bstr,options));
  parse("m"+name+"="+Textformat(Bpos,10)+";"); //200610
  Out;
);
////%Anglemark end////
);
scriptaddstart=0;

ops=["Size=1.5"];
v1=[0,1];v2=[5,4];v3=[5,1];
tmp=Adjust([v1,v2,v3,v1]);
p1=tmp_1;p2=tmp_2;p3=tmp_3;p4=tmp_4;
Listplot("1",tmp);
Anglemark("1",[p3,p1,p2],[6]);
Letter(mag1,"ne","$x$",ops);
tmp=Dotprod(v1-v2,v3-v2)/|v1-v2|/|v3-v2|;
tmp=arccos(tmp);
tmp=Format(tmp*180/pi,1);
Anglemark("2",[p1,p2,p3],[6]);
Letter(mag2,"sw","$"+tmp+"$",ops);
Letter(p1,"w","A",ops);
Letter(p2,"m","B",ops);
Letter(p3,"e","C",ops);
Expr((p1+p3)/2,"s1","5",ops);
Expr((p2+p3)/2,"e1","3",ops);
