if(1==0,//no ketjs on
  Alltextkey(1);
  Setkeystyle();
  inspect(Text51,"text.text",",");
);//no ketjs off

Id=9;
Rec=100;

alpha=pi/40;
cs1=cos(alpha); sn1=sin(alpha);
cs2=cos(2*alpha); sn2=sin(2*alpha);
csL=[[cs2,-sn2],[cs1,-sn1],[1,0],[cs1,sn1],[cs2,sn2]];

Seteditable(9,["ID=","Size=20","Width=150"]);//no ketjs on
Seteditable(10,["","Size=18","Width=60"]);
Seteditable(11,["","Size=18","Width=60"]);
Seteditable(12,["","Size=18","Width=60"]);
Seteditable(13,["","Size=18","Width=60"]);
Seteditable(14,["","Size=18","Width=60"]);
Seteditable(15,["","Size=18","Width=60"]);
Seteditable(62,["","Size=18","Width=700"]);
inspect(Text31,"colorfill",0);
inspect(Text32,"colorfill",0);
inspect(Text33,"colorfill",0);
inspect(A,"ptsize",5.5);//no ketjs off

Eps=0.1;
Inf=10^6;
pO=[0,0];

Makeques():=(
  regional(inf,ang,rot,tmp,tmp1);
  // global Ex,Ca
  inf=10^5;
  Dist=randomint(3)+3;
  tmp=randomint(10)+9;
  Angle=tmp*10;
  ang=Angle*pi/180;
  pA0=Dist*[cos(ang),sin(ang)];
  pC0=Dist*[cos(ang),0];
  pA1=Rotatepoint(pA0,0,pO);
  pC1=Rotatepoint(pC0,0,pO);
  Ex=text([Dist,Angle]);
  if(Angle==90,
    Ca=text([cos(ang),sin(ang),inf]);
  ,
    Ca=text([cos(ang),sin(ang),tan(ang)]);
  );
  cr1=apply(0..10,Dist*[cos(pi/2+#/10*pi/2),sin(pi/2+#/10*pi/2)]);
  Putpoint("A",pA1);
  forall(10..15,Subsedit(#,""));
);

Ansvalue(nstr,dstr):=(
  regional(inf,out,tmp1,tmp2,tmp);
  inf=10^5;
  out=-inf;
  if(length(nstr)+length(dstr)>0,
    if((indexof(nstr,"X")>0)%(indexof(dstr,"X")>0),
      out=inf;
    ,
      if(length(dstr)==0,
        out=parse(Tocindyform(nstr));
      ,
        if(parse(dstr)==0,
          out=inf;
        ,
          out=parse(Tocindyform(nstr+"/"+dstr));
        );
      );
    );
  );
  out;
);

Scoring(pos,size,clr):=(
  regional(ans,cs,sn,tn,inf,eps,
      stans,tmp,tmp1,tmp2);
  //global Ex,Ca
  inf=10^5; eps=0.1;
  cs=-inf; sn=-inf; tn=-inf;
  stans=""; score=0;
  tmp1=Textedit(BoxL_1,"");
  tmp2=Textedit(BoxL_2,"");
  stans=stans+tmp1+","+tmp2;
  cs=Ansvalue(tmp1,tmp2);
  tmp1=Textedit(BoxL_3,"");
  tmp2=Textedit(BoxL_4,"");
  stans=stans+","+tmp1+","+tmp2;
  sn=Ansvalue(tmp1,tmp2);
  tmp1=Textedit(BoxL_5,"");
  tmp2=Textedit(BoxL_6,"");
  stans=stans+","+tmp1+","+tmp2;
  tn=Ansvalue(tmp1,tmp2);
  if(max([cs,sn,tn])==-inf,
    Msgsc=["未入力",pos,size,clr];
     score=-2;
  ,
    ans=parse(Ca);
    stans=text([cs,sn,tn]);
    if(|cs-ans_1|+|sn-ans_2|+|tn-ans_3|<eps,
      Msgsc=["正解",pos,size,clr];
      score=1;
    ,
      Msgsc=["不正解",pos,size,clr];
      score=0;
    );
  );
  [stans,score];
);

Drawfig():=(
  regional(tmp,ang);
  tmp=Crossprod(pA1,A.xy)/(|pA1|*|A.xy|);
  ang=arcsin(tmp);
  pC2=Rotatepoint(pC1,ang,pO);
  pD2=Rotatepoint([2,0],ang,pO);
  Listplot("1",[pO,A.xy,pC2,pO],["dr,2"]);
  Anglemark("1",[pD2,pO,A.xy],[1.3]);
  Listplot("2",[pO,pD2],["dr,1"]);
);

Drwaxes():=(
  regional(tL);
  tL=[];
  forall((-10)..(-1),
    tL=concat(tL,[#/2,text(#)]);
  );
  Htickmark(tL);
  forall((1)..(10),
      tL=concat(tL,[#/2,"e1",text(#)]);
  );
  Vtickmark(tL);
);

Dispinitial():=(
  regional(op,opc,pos,dpos,xpos,tmp);
  pos=4.9;dpos=1;
  xpos=2;
  if(quesgiven==1,
    Drwexpr([0.5,pos+0.5],"\theta="+text(Angle)+"^{\circ}",20);
  );
  Drwexpr([xpos,pos],"\cos\theta=",20);
  Drwexpr([xpos,pos-1*dpos],"\sin\theta=",20);
  Drwexpr([xpos,pos-2*dpos],"\tan\theta=",20);
  xpos=2.85;
  pos=5; 
  tmp=1.3;
  Listplot("cs",[[xpos,pos],[xpos+tmp,pos]],["dr,1.5"]);
  Listplot("sn",[[xpos,pos-dpos],[xpos+tmp,pos-dpos]],["dr,1.5"]);
  Listplot("tn",[[xpos,pos-2*dpos],[xpos+tmp,pos-2*dpos]],["dr,1.5"]);
);
